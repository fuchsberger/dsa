defmodule DsaWeb.UserSessionControllerTest do
  use DsaWeb.ConnCase, async: true

  import Dsa.AccountsFixtures
  import DsaWeb.AccountTranslations

  setup do
    %{user: user_fixture()}
  end

  describe "GET /login" do
    test "renders log in page", %{conn: conn} do
      conn = get(conn, Routes.user_session_path(conn, :new))
      response = html_response(conn, 200)
      assert response =~ t(:heading_sign_in) <> "</h1>"
      assert response =~ t(:register_link) <> "</a>"
      assert response =~ t(:sign_in) <> "</button>"
    end

    test "redirects if already logged in", %{conn: conn, user: user} do
      conn = conn |> log_in_user(user) |> get(Routes.user_session_path(conn, :new))
      assert redirected_to(conn) == "/"
    end
  end

  describe "POST /login" do
    test "logs the user in", %{conn: conn, user: user} do
      conn =
        post(conn, Routes.user_session_path(conn, :create), %{
          "user" => %{"email" => user.email, "password" => valid_user_password()}
        })

      assert get_session(conn, :user_token)
      assert redirected_to(conn) =~ "/"

      # Now do a logged in request and assert on the menu
      # TODO: reenable and fix
      conn = get(conn, "/")
      response = html_response(conn, 200)
      assert response =~ t(:settings) <> "</a>"
      assert response =~ t(:sign_out) <> "</a>"
    end

    test "logs the user in with remember me", %{conn: conn, user: user} do
      conn =
        post(conn, Routes.user_session_path(conn, :create), %{
          "user" => %{
            "email" => user.email,
            "password" => valid_user_password(),
            "remember_me" => "true"
          }
        })

      assert conn.resp_cookies["_dsa_web_user_remember_me"]
      assert redirected_to(conn) =~ "/"
    end

    test "logs the user in with return to", %{conn: conn, user: user} do
      conn =
        conn
        |> init_test_session(user_return_to: "/foo/bar")
        |> post(Routes.user_session_path(conn, :create), %{
          "user" => %{
            "email" => user.email,
            "password" => valid_user_password()
          }
        })

      assert redirected_to(conn) == "/foo/bar"
    end

    test "emits error message with invalid credentials", %{conn: conn, user: user} do
      conn =
        post(conn, Routes.user_session_path(conn, :create), %{
          "user" => %{"email" => user.email, "password" => "invalid_password"}
        })

      response = html_response(conn, 200)
      assert response =~ t(:heading_sign_in)
      assert response =~ t(:invalid_email_or_password)
    end

    test "emits error message when account is not confirmed", %{conn: conn} do
      user = user_fixture(%{}, confirmed: false)

      conn =
        post(conn, Routes.user_session_path(conn, :create), %{
          "user" => %{
            "email" => user.email,
            "password" => valid_user_password(),
            "remember_me" => "true"
          }
        })

      response = html_response(conn, 200)
      assert response =~ t(:heading_sign_in) <>"</h1>"
      assert response =~ t(:confirm_before_signin)
    end

    test "emits error message when account is blocked", %{conn: conn} do
      {:ok, user} = user_fixture() |> Dsa.Accounts.block_user()

      conn =
        post(conn, Routes.user_session_path(conn, :create), %{
          "user" => %{
            "email" => user.email,
            "password" => valid_user_password(),
            "remember_me" => "true"
          }
        })

      response = html_response(conn, 200)
      assert response =~ t(:heading_sign_in) <> "</h1>"
      assert response =~ t(:blocked_message)
    end
  end

  describe "DELETE /users/log_out" do
    test "logs the user out", %{conn: conn, user: user} do
      conn = conn |> log_in_user(user) |> delete(Routes.user_session_path(conn, :delete))
      assert redirected_to(conn) == "/"
      refute get_session(conn, :user_token)
      assert get_flash(conn, :info) =~ t(:logged_out)
    end

    test "succeeds even if the user is not logged in", %{conn: conn} do
      conn = delete(conn, Routes.user_session_path(conn, :delete))
      assert redirected_to(conn) == "/"
      refute get_session(conn, :user_token)
      assert get_flash(conn, :info) =~ t(:logged_out)
    end
  end
end
